apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'codenarc'

group = 'com.github.mibexsoftware' // necessary for jitpack

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            groupId 'ch.mibex.bamboo.plandsl'
            artifactId 'plan-dsl-lib'
            version version
            artifact groovydocsJar
            artifact sourcesJar
        }
    }
}

sourceSets {
    ast {
        groovy {
            srcDir 'src/ast/groovy'
        }
        resources {
            srcDir 'src/ast/resources'
        }
    }
    main.compileClasspath += sourceSets.ast.output
    test.compileClasspath += sourceSets.ast.output
}



task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocsJar(type: Jar, dependsOn: 'groovydoc') {
    from project.groovydoc.destinationDir
    classifier = 'groovydoc'
}

codenarc {
    toolVersion = '0.25.2'
    configFile = file('config/codenarc/rules.groovy')
    sourceSets = [project.sourceSets.main] // currently restricted to src/main/groovy/** files
}

repositories {
    mavenCentral()
}

dependencies {
    astCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile group: 'org.apache.ant', name: 'ant', version: '1.8.2'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
}
